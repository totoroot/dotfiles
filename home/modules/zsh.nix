{ config, options, pkgs, lib, ... }:

with lib;
with lib.my;
let
  cfg = config.modules.home.zsh;
  dotfilesConfigDir = "${config.xdg.configHome}/dotfiles/config";
in
{
  options.modules.home.zsh = with types; {
    enable = mkBoolOpt false;

    aliases = mkOpt (attrsOf (either str path)) { };

    rcInit = mkOpt' lines "" ''
      ZSH lines to be written to ~/.config/zsh/extra.zshrc and sourced by ~/.zshrc
    '';
    envInit = mkOpt' lines "" ''
      ZSH lines to be written to ~/.config/zsh/extra.zshenv and sourced by ~/.zshenv
    '';

    rcFiles = mkOpt (listOf (either str path)) [ ];
    envFiles = mkOpt (listOf (either str path)) [ ];
  };

  config = mkIf cfg.enable {
    programs.zsh = {
      enable = true;
      # enableCompletion = true;
      dotDir = "${config.home.homeDirectory}/.config/zsh/";
      # I init completion myself, because enableGlobalCompInit initializes it
      # too soon, which means commands initialized later in my config won't get
      # completion, and running compinit twice is slow.
      completionInit = "";
    };

    home.packages = with pkgs; [
      zsh
      # Additional completion definitions for zsh
      zsh-completions
      # ZSH completions for Nix, NixOS, and NixOps
      nix-zsh-completions
      # Fish shell history-substring-search for ZSH
      zsh-history-substring-search
      # Fast cd command that learns your habits
      zoxide
    ];

    home.sessionVariables = {
      ZDOTDIR = "${config.home.homeDirectory}/.config/zsh/";
      ZSH_CACHE = "${config.home.homeDirectory}/.local/cache/zsh";
      ZGENOM_DIR = "${config.home.homeDirectory}/.local/share/zsh";
      ZGENOM_SOURCE = "$ZGENOM_DIR/zgenom.zsh";
    };

    modules.home.configSymlinks.enable = true;
    modules.home.configSymlinks.sourceDir = dotfilesConfigDir;
    modules.home.configSymlinks.destination = ".config";
    modules.home.configSymlinks.entries =
      (map (name: "zsh/${name}") [
        "aliases.zsh"
        "completion.zsh"
        "config.zsh"
        "extract.zsh"
        "keybinds.zsh"
        "prompt.zsh"
        ".zshrc"
        # ".zshenv"
      ]);

    home.file = {
      ".zshenv".text = ''
        export XDG_CONFIG_HOME="$HOME/.config"
        export XDG_CACHE_HOME="$HOME/.cache"
        export XDG_DATA_HOME="$HOME/.local/share"
        export XDG_STATE_HOME="$HOME/.local/state"

        export ZDOTDIR="$XDG_CONFIG_HOME/zsh"
        export ZSH_CACHE="$XDG_CACHE_HOME/zsh"
        export ZGENOM_DIR="$XDG_DATA_HOME/zsh"
        export ZGENOM_SOURCE="$ZGENOM_DIR/zgenom.zsh"
      '';
      ".zshenv".force = true;

      # Why am I creating extra.zsh{rc,env} when I could be using extraInit?
      # Because extraInit generates those files in /etc/profile, and mine just
      # write the files to ~/.config/zsh; where it's easier to edit and tweak
      # them in case of issues or when experimenting.
      ".config/zsh/extra.zshrc".text =
        let aliasLines = mapAttrsToList (n: v: "alias ${n}=\"${v}\"") cfg.aliases;
        in ''
          # This file was autogenerated, do not edit it!
          ${concatStringsSep "\n" aliasLines}
          ${concatMapStrings (path: "source '${path}'\n") cfg.rcFiles}
          ${cfg.rcInit}
        '';

      ".config/zsh/extra.zshenv".text = ''
        # This file is autogenerated, do not edit it!
        ${concatMapStrings (path: "source '${path}'\n") cfg.envFiles}
        ${cfg.envInit}
      '';
    };

    # home.activation.cleanupZgenom = "rm -fv $XDG_CACHE_HOME/zsh/*";
  };
}
