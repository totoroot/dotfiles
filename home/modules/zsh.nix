{ config, options, pkgs, lib, ... }:

with lib;
with lib.my;
let
  cfg = config.modules.home.zsh;
  dotfilesConfigDir = "${config.xdg.configHome}/dotfiles/config";
in
{
  options.modules.home.zsh = with types; {
    enable = mkBoolOpt false;

    aliases = mkOpt (attrsOf (either str path)) { };

    rcInit = mkOpt' lines "" ''
      Zsh lines to be written to $XDG_CONFIG_HOME/zsh/extra.zshrc and sourced by
      $XDG_CONFIG_HOME/zsh/.zshrc
    '';
    envInit = mkOpt' lines "" ''
      Zsh lines to be written to $XDG_CONFIG_HOME/zsh/extra.zshenv and sourced
      by $XDG_CONFIG_HOME/zsh/.zshenv
    '';

    rcFiles = mkOpt (listOf (either str path)) [ ];
    envFiles = mkOpt (listOf (either str path)) [ ];
  };

  config = mkIf cfg.enable {
    modules.home.configSymlinks.enable = true;
    modules.home.configSymlinks.sourceDir = dotfilesConfigDir;
    modules.home.configSymlinks.destination = ".config";
    modules.home.configSymlinks.entries = [
      "zsh/.zshrc"
      "zsh/.zshenv"
      "zsh/aliases.zsh"
      "zsh/completion.zsh"
      "zsh/config.zsh"
      "zsh/extract.zsh"
      "zsh/keybinds.zsh"
    ];

    programs.zsh = {
      enable = true;
      enableCompletion = true;
      # I init completion myself, because enableGlobalCompInit initializes it
      # too soon, which means commands initialized later in my config won't get
      # completion, and running compinit twice is slow.
      completionInit = "";
    };

    home.packages = with pkgs; [
      zsh
      # ZSH completions for Nix, NixOS, and NixOps
      nix-zsh-completions
      # Fish shell history-substring-search for ZSH
      zsh-history-substring-search
    ];

    home.sessionVariables = {
      ZDOTDIR = "$XDG_CONFIG_HOME/zsh";
      ZSH_CACHE = "$XDG_CACHE_HOME/zsh";
      ZGENOM_DIR = "$XDG_DATA_HOME/zsh";
      ZGENOM_SOURCE = "$ZGENOM_DIR/zgenom.zsh";
    };

    xdg.configFile = {
      # Keep ~/.config/zsh writable for tools that drop completions at runtime.
      # Only manage the explicit files we control.

      # Why am I creating extra.zsh{rc,env} when I could be using extraInit?
      # Because extraInit generates those files in /etc/profile, and mine just
      # write the files to ~/.config/zsh; where it's easier to edit and tweak
      # them in case of issues or when experimenting.
      "zsh/extra.zshrc".text =
        let aliasLines = mapAttrsToList (n: v: "alias ${n}=\"${v}\"") cfg.aliases;
        in ''
          # This file was autogenerated, do not edit it!
          ${concatStringsSep "\n" aliasLines}
          ${concatMapStrings (path: "source '${path}'\n") cfg.rcFiles}
          ${cfg.rcInit}
        '';

      "zsh/extra.zshenv".text = ''
        # This file is autogenerated, do not edit it!
        ${concatMapStrings (path: "source '${path}'\n") cfg.envFiles}
        ${cfg.envInit}
      '';
    };

    # home.activation.cleanupZgenom = "rm -fv $XDG_CACHE_HOME/zsh/*";
  };
}
