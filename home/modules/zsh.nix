{ config, options, pkgs, lib, ... }:

with lib;
with lib.my;
let
  cfg = config.modules.home.zsh;
in
{
  options.modules.home.zsh = with types; {
    enable = mkBoolOpt false;

    aliases = mkOpt (attrsOf (either str path)) { };

    rcInit = mkOpt' lines "" ''
      ZSH lines to be written to ~/.config/zsh/extra.zshrc and sourced by ~/.zshrc
    '';
    envInit = mkOpt' lines "" ''
      ZSH lines to be written to ~/.config/zsh/extra.zshenv and sourced by ~/.zshenv
    '';

    rcFiles = mkOpt (listOf (either str path)) [ ];
    envFiles = mkOpt (listOf (either str path)) [ ];
  };

  config = mkIf cfg.enable {
    programs.zsh = {
      enable = true;
      # enableCompletion = true;
      dotDir = "${config.home.homeDirectory}/.config/zsh/";
      # I init completion myself, because enableGlobalCompInit initializes it
      # too soon, which means commands initialized later in my config won't get
      # completion, and running compinit twice is slow.
      completionInit = "";
    };

    home.packages = with pkgs; [
      zsh
      # Additional completion definitions for zsh
      zsh-completions
      # ZSH completions for Nix, NixOS, and NixOps
      nix-zsh-completions
      # Fish shell history-substring-search for ZSH
      zsh-history-substring-search
      # Fast cd command that learns your habits
      zoxide
    ];


    # XDG_CONFIG_HOME
    #     Where user-specific configurations should be written (analogous to /etc).
    #     Should default to $HOME/.config.
    # XDG_CACHE_HOME
    #     Where user-specific non-essential (cached) data should be written (analogous to /var/cache).
    #     Should default to $HOME/.cache.
    # XDG_DATA_HOME
    #     Where user-specific data files should be written (analogous to /usr/share).
    #     Should default to $HOME/.local/share.
    # XDG_STATE_HOME
    #     Where user-specific state files should be written (analogous to /var/lib).
    #     Should default to $HOME/.local/state.

    home.sessionVariables = {
      ZDOTDIR = "${config.home.homeDirectory}/.config/zsh/";
      ZSH_CACHE = "${config.home.homeDirectory}/.local/cache/zsh";
      ZGENOM_DIR = "${config.home.homeDirectory}/.local/share/zsh";
      ZGENOM_SOURCE = "$ZGENOM_DIR/zgenom.zsh";
    };

    modules.home.configSymlinks.entries =
      (map (name: "zsh/${name}") [
        "aliases.zsh"
        "completion.zsh"
        "completions"
        "config.zsh"
        "extract.zsh"
        "keybinds.zsh"
        "prompt.zsh"
        ".zshrc"
        # ".zshenv"
      ]);

    home.file = {
      # Why am I creating extra.zsh{rc,env} when I could be using extraInit?
      # Because extraInit generates those files in /etc/profile, and mine just
      # write the files to ~/.config/zsh; where it's easier to edit and tweak
      # them in case of issues or when experimenting.
      ".config/zsh/extra.zshrc".text =
        let aliasLines = mapAttrsToList (n: v: "alias ${n}=\"${v}\"") cfg.aliases;
        in ''
          # This file was autogenerated, do not edit it!
          ${concatStringsSep "\n" aliasLines}
          ${concatMapStrings (path: "source '${path}'\n") cfg.rcFiles}
          ${cfg.rcInit}
        '';

      ".config/zsh/extra.zshenv".text = ''
        # This file is autogenerated, do not edit it!
        ${concatMapStrings (path: "source '${path}'\n") cfg.envFiles}
        ${cfg.envInit}
      '';
    };

    # home.activation.cleanupZgenom = "rm -fv $XDG_CACHE_HOME/zsh/*";
  };
}
